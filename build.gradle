
buildscript {
    ext {
        querydslVersion    = "4.1.4"
        metaModelsSourcesDir = file("metamodels")
    }
}

plugins {
    id 'java'
    id 'application'
    id 'distribution'
}
compileJava.options.encoding = 'UTF-8'


configurations {
    querydslapt
}

sourceSets {
    main {
        java {
            srcDir metaModelsSourcesDir
        }
    }
}

task querymodels(type: JavaCompile, group: 'build') {
    doFirst {
        delete metaModelsSourcesDir;
        metaModelsSourcesDir.mkdirs();
    }

    classpath = configurations.compile + configurations.querydslapt
    destinationDir = metaModelsSourcesDir

    source = sourceSets.main.java
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor",
            "-s", metaModelsSourcesDir,
            "-encoding", "UTF-8"
    ]

}


repositories {
    mavenCentral()
    jcenter()
}


group = 'de.nerogar'
version = '1.0.0'
sourceCompatibility = 13
targetCompatibility = 13

dependencies {
    // jda (discord api)
    compile ('net.dv8tion:JDA:3.8.3_464') {
        // all audio is pre encoded with opus, we don't need the opus module
        exclude module: 'opus-java'
    }

    // jackson
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.7'

    // vorbis java
    compile group: 'org.gagravarr', name: 'vorbis-java-core', version: '0.8'

    // persistance
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.28.0'
}

task createRun(type: Copy) {
    from 'src/main/dist'
    into 'run'
}

run.workingDir 'run'
run.dependsOn createRun

mainClassName = "de.nerogar.sandstormBot.Main"
